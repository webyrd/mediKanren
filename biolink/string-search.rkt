#lang racket/base
(provide
  string/searchable
  suffix:corpus->index
  suffix:corpus-find*
  string:corpus->index
  string:corpus-find*)
(require
  "repr.rkt"
  racket/list
  racket/string
  racket/unsafe/ops
  racket/vector)

(define (msd-radix-sort vs len dref v<?)
  (define vvs (list->vector vs))
  (define out (make-vector (vector-length vvs)))
  (define pos-count 127)
  (define pos (make-vector pos-count))
  (define (++pos! i) (unsafe-vector*-set!
                       pos i (+ 1 (unsafe-vector*-ref pos i))))
  (let loop ((bi 0) (vi 0) (vend (vector-length vvs)) (vs vvs) (out out))
    (cond
      ((< (- vend vi) 100) (vector-sort! vs v<? vi vend)
                           (when (= 0 (bitwise-and bi 1))
                             (vector-copy! out vi vs vi vend)))
      (else (vector-fill! pos 0)
            (define done-pos
              (let i-loop ((i vi) (done-pos vi))
                (cond ((< i vend)
                       (define v (unsafe-vector*-ref vs i))
                       (cond ((<= (len v) bi)
                              (unsafe-vector*-set! out done-pos v)
                              (i-loop (+ 1 i) (+ 1 done-pos)))
                             (else (define d (dref v bi))
                                   (when (< d pos-count) (++pos! d))
                                   (i-loop (+ 1 i) done-pos))))
                      (else done-pos))))
            (define initial-big-pos
              (let p-loop ((d 0) (p done-pos))
                (cond ((< d pos-count)
                       (define p^ (+ p (unsafe-vector*-ref pos d)))
                       (unsafe-vector*-set! pos d p)
                       (p-loop (+ 1 d) p^))
                      (else p))))
            (define big-pos
              (let i-loop ((i vi) (big-pos initial-big-pos))
                (cond ((< i vend)
                       (define v (unsafe-vector*-ref vs i))
                       (cond ((< bi (len v))
                              (define d (dref v bi))
                              (cond ((< d pos-count)
                                     (unsafe-vector*-set!
                                       out (unsafe-vector*-ref pos d) v)
                                     (++pos! d)
                                     (i-loop (+ 1 i) big-pos))
                                    (else (unsafe-vector*-set! out big-pos v)
                                          (i-loop (+ 1 i) (+ 1 big-pos)))))
                             (else (i-loop (+ 1 i) big-pos))))
                      (else big-pos))))
            (when (< initial-big-pos vend)
              (vector-sort! out v<? initial-big-pos vend)
              (when (= 1 (bitwise-and bi 1))
                (vector-copy! vs initial-big-pos out initial-big-pos big-pos)))
            (when (= 1 (bitwise-and bi 1))
              (vector-copy! vs vi out vi done-pos))
            (let d-loop ((i done-pos))
              (when (< i initial-big-pos)
                (define d (dref (unsafe-vector*-ref out i) bi))
                (let end-loop ((end i) (offset 1))
                  (define next (+ end offset))
                  (cond
                    ((and (< next initial-big-pos)
                          (= d (dref (unsafe-vector*-ref out next) bi)))
                     (end-loop next (arithmetic-shift offset 1)))
                    (else (let end-loop
                            ((end end) (offset (arithmetic-shift offset -1)))
                            (define next (+ end offset))
                            (if (= offset 0)
                              (begin (loop (+ 1 bi) i (+ 1 end) out vs)
                                     (d-loop (+ 1 end)))
                              (end-loop
                                (cond ((and (< next initial-big-pos)
                                            (= d (dref (unsafe-vector*-ref
                                                         out next) bi)))
                                       next)
                                      (else end))
                                (arithmetic-shift offset -1))))))))))))
  out)

(define (nlist-intersection nlists)
  (let loop ((i** (map (lambda (nlist) (sort nlist <)) nlists)))
    (cond ((ormap null? i**) '())
          (else (define i0* (map car i**))
                (define next (apply max i0*))
                (if (andmap (lambda (i) (= i next)) i0*)
                  (cons next (loop (map cdr i**)))
                  (loop (map (lambda (i*) (dropf i* (lambda (i) (< i next))))
                             i**)))))))

(define i0              (char->integer #\0))
(define i9              (char->integer #\9))
(define iA              (char->integer #\A))
(define iZ              (char->integer #\Z))
(define (searchable? c) (and (<= i0 c) (<= c iZ) (or (<= c i9) (<= iA c))))
(define (string/searchable s)
  (define cs (map char->integer (string->list (string-upcase s))))
  (list->string (map integer->char (filter searchable? cs))))
(define (suffix->string corpus s)
  (substring (vector-ref corpus (car s)) (cdr s)))
(define (suffix-string-length corpus s)
  (- (string-length (vector-ref corpus (car s))) (cdr s)))
(define (suffix-string-ref corpus s i)
  (char->integer (string-ref (unsafe-vector*-ref corpus (car s))
                             (+ (cdr s) i))))
(define (string<?/suffixes a ai b bi)
  (define alen (- (string-length a) ai))
  (define blen (- (string-length b) bi))
  (let loop ((k (min alen blen)) (ai ai) (bi bi))
    (cond ((= k 0)                                      (< alen blen))
          ((char<? (string-ref a ai) (string-ref b bi)) #t)
          ((char>? (string-ref a ai) (string-ref b bi)) #f)
          (else (loop (- k 1) (+ ai 1) (+ bi 1))))))
(define (suffix<?/corpus corpus a b)
  (string<?/suffixes (vector-ref corpus (car a)) (cdr a)
                     (vector-ref corpus (car b)) (cdr b)))

(define (suffix:corpus->index corpus)
  (define suffixes
    (foldl (lambda (i all)
             (foldl (lambda (j all) (cons (cons i j) all))
                    all (range (string-length (vector-ref corpus i)))))
           '() (range (vector-length corpus))))
  (define (suffix<? a b)    (suffix<?/corpus corpus a b))
  (define (suffix-length s) (suffix-string-length corpus s))
  (define (suffix-ref s i)  (suffix-string-ref corpus s i))
  (msd-radix-sort suffixes suffix-length suffix-ref suffix<?))

(define (suffix:corpus-find corpus index str)
  (define needle (string/searchable str))
  (define (compare si needle)
    (define hay (suffix->string corpus (suffix-key-ref index si)))
    (cond ((string-prefix? hay needle) 0)
          ((string<? hay needle)      -1)
          (else                        1)))
  ;; Find a point in the desired range...
  (let find-range ((start 0) (end (suffix-key-count index)))
    (cond ((< start end)
           (define mid (+ start (quotient (- end start) 2)))
           (case (compare mid needle)
             ((-1) (find-range (+ 1 mid) end))
             (( 1) (find-range start mid))
             (( 0) ;; ... then find the start and end of that range.
              (define rstart
                (let loop ((start start) (end mid))
                  (cond ((< start end)
                         (define mid (+ start (quotient (- end start) 2)))
                         (case (compare mid needle)
                           ((-1) (loop (+ 1 mid) end))
                           (( 0) (loop start mid))
                           (else (error "rstart: this shouldn't happen."))))
                        (else end))))
              (define rend
                (let loop ((start (+ 1 mid)) (end end))
                  (cond ((< start end)
                         (define mid (+ start (quotient (- end start) 2)))
                         (case (compare mid needle)
                           ((1) (loop start mid))
                           ((0) (loop (+ 1 mid) end))
                           (else (error "rend: this shouldn't happen."))))
                        (else end))))
              (remove-duplicates
                (map (lambda (i) (car (suffix-key-ref index i)))
                     (range rstart rend))))))
          (else '()))))

(define (suffix:corpus-find* corpus index str*)
  (nlist-intersection
    (map (lambda (s) (suffix:corpus-find corpus index s)) str*)))

(define (string:corpus->index corpus)
  (define ixs (range (vector-length corpus)))
  (define (ix<? a b) (string<? (vector-ref corpus a) (vector-ref corpus b)))
  (define (ix-length ix) (string-length (vector-ref corpus ix)))
  (define (ix-ref ix i)  (char->integer (string-ref (vector-ref corpus ix) i)))
  (msd-radix-sort ixs ix-length ix-ref ix<?))

(define (string:corpus-find corpus index needle)
  (define (compare si needle)
    (define hay (vector-ref corpus (vector-ref index si)))
    (cond ((string=? hay needle)  0)
          ((string<? hay needle) -1)
          (else                   1)))
  ;; Find a point in the desired range...
  (let find-range ((start 0) (end (vector-length index)))
    (cond ((< start end)
           (define mid (+ start (quotient (- end start) 2)))
           (case (compare mid needle)
             ((-1) (find-range (+ 1 mid) end))
             (( 1) (find-range start mid))
             (( 0) ;; ... then find the start and end of that range.
              (define rstart
                (let loop ((start start) (end mid))
                  (cond ((< start end)
                         (define mid (+ start (quotient (- end start) 2)))
                         (case (compare mid needle)
                           ((-1) (loop (+ 1 mid) end))
                           (( 0) (loop start mid))
                           (else (error "rstart: this shouldn't happen."))))
                        (else end))))
              (define rend
                (let loop ((start (+ 1 mid)) (end end))
                  (cond ((< start end)
                         (define mid (+ start (quotient (- end start) 2)))
                         (case (compare mid needle)
                           ((1) (loop start mid))
                           ((0) (loop (+ 1 mid) end))
                           (else (error "rend: this shouldn't happen."))))
                        (else end))))
              (remove-duplicates (map (lambda (i) (vector-ref index i))
                                      (range rstart rend))))))
          (else '()))))

(define (string:corpus-find* corpus index str*)
  (remove-duplicates
    (sort (append* (map (lambda (s) (string:corpus-find corpus index s)) str*))
          <)))
